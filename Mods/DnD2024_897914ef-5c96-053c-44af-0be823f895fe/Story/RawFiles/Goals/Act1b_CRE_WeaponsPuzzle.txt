Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB to organise the elements of the puzzle by Abbot
DB_CRE_WeaponPuzzle_ContainersAndWeapons(S_CRE_WeaponsPuzzle_Abbot1_Trigger_e6f01db1-6d5f-4c12-a194-4f4c821940ee, "Maces", CRE_WeaponsPuzzle_HasSpear_d9c14531-51c5-4796-b29b-f44b1ab384c7);
DB_CRE_WeaponPuzzle_ContainersAndWeapons(S_CRE_WeaponsPuzzle_Abbot2_Trigger_1ece034d-d85d-46c6-9116-5770b0d86372, "Longswords", CRE_WeaponsPuzzle_HasLongsword_3cc7a940-0ecd-44dd-b04c-ebfe4562e56e);
DB_CRE_WeaponPuzzle_ContainersAndWeapons(S_CRE_WeaponsPuzzle_Abbot3_Trigger_88fbe285-708f-49fd-8da5-16bcdd64fcba, "Warhammers", CRE_WeaponsPuzzle_HasWarhammer_e813343c-bd7e-41c9-b0d8-db5b9ea57dbf);
DB_CRE_WeaponPuzzle_ContainersAndWeapons(S_CRE_WeaponsPuzzle_Abbot4_Trigger_54605b62-84d6-4ba0-be77-d984627bdc6e, "Battleaxes", CRE_WeaponsPuzzle_HasBattleaxe_3049751e-9841-4b34-b8ee-8950d80bf8a9);


DB_CRE_WeaponPuzzle_CeremonialWeapons(S_CRE_Abbot1_Weapon_fce42526-f518-4727-91f6-c41c234fa053);
DB_CRE_WeaponPuzzle_CeremonialWeapons(S_CRE_Abbot2_Weapon_1edc931d-6471-49c2-b2f4-d3ab0308ac09);
DB_CRE_WeaponPuzzle_CeremonialWeapons(S_CRE_Abbot3_Weapon_4dffb833-a590-47ea-8c80-71a430b5e099);
DB_CRE_WeaponPuzzle_CeremonialWeapons(S_CRE_Abbot4_Weapon_0698b266-b7f8-4593-9063-f1189082c6e1);


DB_CRE_WeaponPuzzle_Lights(S_CRE_WeaponsPuzzle_Abbot1_Trigger_e6f01db1-6d5f-4c12-a194-4f4c821940ee, S_CRE_Abbot1_Crystal1_On_0689090b-3d74-4575-bfc1-f887b7599d71, S_CRE_Abbot1_Crystal2_On_3d6c7688-4e4a-4924-9bf3-6519cc04f4ce, S_CRE_Abbot1_Crystal1_Off_daaa88d6-4cbc-4829-beb6-afe62b322dda, S_CRE_Abbot1_Crystal2_Off_7d1d0405-7f50-4a53-9538-d62e97554e67);
DB_CRE_WeaponPuzzle_Lights(S_CRE_WeaponsPuzzle_Abbot2_Trigger_1ece034d-d85d-46c6-9116-5770b0d86372, S_CRE_Abbot2_Crystal1_On_34b048f8-3214-46ac-8da5-48c12c73789b, S_CRE_Abbot2_Crystal2_On_043bad0c-b5d3-4541-abdb-883c5dfb7c3f, S_CRE_Abbot2_Crystal1_Off_9be44bdc-28c2-4a76-82fc-5cff367e430f, S_CRE_Abbot2_Crystal2_Off_0a6c6534-71c7-413d-a6db-4356edc9600f);
DB_CRE_WeaponPuzzle_Lights(S_CRE_WeaponsPuzzle_Abbot3_Trigger_88fbe285-708f-49fd-8da5-16bcdd64fcba, S_CRE_Abbot3_Crystal1_On_ad12c4cb-464e-454a-a116-e0e17f378b68, S_CRE_Abbot3_Crystal2_On_6984a16f-0489-4dcf-a37b-65365a9fff57, S_CRE_Abbot3_Crystal1_Off_37f1bbca-a639-401d-8bdd-c7d57af783ad, S_CRE_Abbot3_Crystal2_Off_01aae0ef-5cc7-4cc2-b018-139feaf07be6);
DB_CRE_WeaponPuzzle_Lights(S_CRE_WeaponsPuzzle_Abbot4_Trigger_54605b62-84d6-4ba0-be77-d984627bdc6e, S_CRE_Abbot4_Crystal1_On_0f6c9708-154d-4452-9d4b-345a049f3190, S_CRE_Abbot4_Crystal2_On_0ca3266a-2938-46c2-9636-7b3d61e6db83, S_CRE_Abbot4_Crystal1_Off_8749336c-10b8-48b9-9cc7-22d8be0511a9, S_CRE_Abbot4_Crystal2_Off_255ea95c-153f-4808-8238-da3c225fc581);

//Turn on the crystals (the ones that are pre-solved)
PROC_SetOnStage(S_CRE_Abbot2_Crystal1_On_34b048f8-3214-46ac-8da5-48c12c73789b, 1);
PROC_SetOnStage(S_CRE_Abbot2_Crystal2_On_043bad0c-b5d3-4541-abdb-883c5dfb7c3f, 1);
PROC_SetOnStage(S_CRE_Abbot2_Crystal1_Off_9be44bdc-28c2-4a76-82fc-5cff367e430f, 0);
PROC_SetOnStage(S_CRE_Abbot2_Crystal2_Off_0a6c6534-71c7-413d-a6db-4356edc9600f, 0);

//Turn off the crystals (the ones that are not pre-solved)
PROC_SetOnStage(S_CRE_Abbot1_Crystal1_On_0689090b-3d74-4575-bfc1-f887b7599d71, 0);
PROC_SetOnStage(S_CRE_Abbot1_Crystal2_On_3d6c7688-4e4a-4924-9bf3-6519cc04f4ce, 0);
PROC_SetOnStage(S_CRE_Abbot3_Crystal1_On_ad12c4cb-464e-454a-a116-e0e17f378b68, 0);
PROC_SetOnStage(S_CRE_Abbot3_Crystal2_On_6984a16f-0489-4dcf-a37b-65365a9fff57, 0);
PROC_SetOnStage(S_CRE_Abbot4_Crystal1_On_0f6c9708-154d-4452-9d4b-345a049f3190, 0);
PROC_SetOnStage(S_CRE_Abbot4_Crystal2_On_0ca3266a-2938-46c2-9636-7b3d61e6db83, 0);

//Triggers
TriggerRegisterForItems(S_CRE_WeaponsPuzzle_Abbot1_Trigger_e6f01db1-6d5f-4c12-a194-4f4c821940ee);
TriggerRegisterForItems(S_CRE_WeaponsPuzzle_Abbot2_Trigger_1ece034d-d85d-46c6-9116-5770b0d86372);
TriggerRegisterForItems(S_CRE_WeaponsPuzzle_Abbot3_Trigger_88fbe285-708f-49fd-8da5-16bcdd64fcba);
TriggerRegisterForItems(S_CRE_WeaponsPuzzle_Abbot4_Trigger_54605b62-84d6-4ba0-be77-d984627bdc6e);

//Hiding the reward
PROC_SetOnStage(S_CRE_WeaponsPuzzle_Bag_fe520e48-2caa-421d-948c-30fa84c690ce, 0);

//Sword has been pre-placed into the right container
SetFlag((FLAG)CRE_WeaponsPuzzle_HasLongsword_3cc7a940-0ecd-44dd-b04c-ebfe4562e56e, NULL_00000000-0000-0000-0000-000000000000);
ApplyStatus(S_CRE_Abbot2_Weapon_1edc931d-6471-49c2-b2f4-d3ab0308ac09, "LIGHT", -1.0, 0, NULL_00000000-0000-0000-0000-000000000000);

//Dialogs
DB_Dialogs(S_CRE_PuzzleGlassWindow_40aac2f6-3936-401d-a0c3-329d704088d7, (DIALOGRESOURCE)CRE_WeaponsPuzzle_GlassWindow_82cff210-2ce2-5692-07a6-7556c60f9341);

//Plaques
DB_ItemDialog_NarratorAD(S_CRE_Abbot1_Plaque_8a99444a-2168-4183-8e14-67e340da7d4f, (DIALOGRESOURCE)CRE_WeaponsPuzzle_AD_Abbot1Plaque_21fbd278-a189-bd33-4961-c4654047b823);
DB_ItemDialog_NarratorAD(S_CRE_Abbot2_Plaque_7bdb282d-9bb5-4cdc-a134-0bd6892cf41c, (DIALOGRESOURCE)CRE_WeaponsPuzzle_AD_Abbot2Plaque_00de5d66-d352-d92a-d580-379ce8e0b1a7);
DB_ItemDialog_NarratorAD(S_CRE_Abbot3_Plaque_8088ece6-6069-4c00-a972-a8c0e5d5a473, (DIALOGRESOURCE)CRE_WeaponsPuzzle_AD_Abbot3Plaque_a29fb01b-7078-a06b-b73c-a9e027a37aab);
DB_ItemDialog_NarratorAD(S_CRE_Abbot4_Plaque_abf40ddb-27e1-4d5e-acfc-726dee84f440, (DIALOGRESOURCE)CRE_WeaponsPuzzle_AD_Abbot4Plaque_da329836-d3e5-443b-9819-6667e8162f8e);
KBSECTION
//Accept the weapon of the correct type
IF
ItemEnteredTrigger(_Weapon, _Trigger, _)
AND
DB_CRE_WeaponPuzzle_ContainersAndWeapons(_Trigger, _WeaponType, _Flag)
AND
Exists(_Weapon,1)
AND
IsEquipmentWithProficiency(_Weapon, _WeaponType, 1)
THEN
SetFlag((FLAG)_Flag, NULL_00000000-0000-0000-0000-000000000000);
ApplyStatus(_Weapon, "LIGHT", -1.0, 0, NULL_00000000-0000-0000-0000-000000000000);
PROC_CRE_WeaponsPuzzle_TurnOnLight((TRIGGER)_Trigger);

//If the weapon was also non-ceremonial, fire an AD
IF
ItemEnteredTrigger(_Weapon, _Trigger, _Player)
AND
DB_CRE_WeaponPuzzle_ContainersAndWeapons(_Trigger, _WeaponType, _)
AND
Exists(_Weapon,1)
AND
IsEquipmentWithProficiency(_Weapon, _WeaponType, 1)
AND
NOT DB_CRE_WeaponPuzzle_CeremonialWeapons(_Weapon)
AND
QRY_OncePerUserAndNearbyPlayers((CHARACTER)_Player,"CRE_WeaponsPuzzle_FireGenericWeaponsPAD")
THEN
PROC_TryStartAD((DIALOGRESOURCE)CRE_WeaponsPuzzle_PAD_GenericWeaponReaction_484e3320-1269-a802-029c-59774bcf68ea, _Player);

//Throw the incorrect weapon back at the player, dealing them damage (once we can cast spells from objects this can use a throw spell
IF
ItemEnteredTrigger(_Weapon, _Trigger, _Player)
AND
DB_CRE_WeaponPuzzle_ContainersAndWeapons(_Trigger, _WeaponType, _)
AND
_Player != NULL_00000000-0000-0000-0000-000000000000
AND
Exists(_Weapon,1)
AND
NOT IsEquipmentWithProficiency(_Weapon, _WeaponType, 1)
AND
GetPosition(_Player, _x, _yTemp, _z)
AND
RealSum(_yTemp, 1.0, _y)
THEN
ItemMoveToPosition(_Weapon, _x, _y, _z, 10.0, 5.0, "",1);
ApplyDamage(_Player, 5, "Piercing", _Weapon);
ApplyStatus(_Player, "KNOCKED_DOWN", 2.0, 2,_Weapon);

//Removing the correct item will end the effect
IF
ItemLeftTrigger(_Weapon, _Trigger, _Player)
AND
DB_CRE_WeaponPuzzle_ContainersAndWeapons(_Trigger, _WeaponType, _Flag)
AND
Exists(_Weapon,1)
AND
IsEquipmentWithProficiency(_Weapon, _WeaponType, 1)
THEN
ClearFlag((FLAG)_Flag, NULL_00000000-0000-0000-0000-000000000000);
RemoveStatus(_Weapon, "LIGHT", NULL_00000000-0000-0000-0000-000000000000);
PROC_CRE_WeaponsPuzzle_RemovedWeaponReaction((CHARACTER)_Player);
PROC_CRE_WeaponsPuzzle_TurnOffLight((TRIGGER)_Trigger);

PROC
PROC_CRE_WeaponsPuzzle_RemovedWeaponReaction((CHARACTER)_Player)
AND
_Player != NULL_00000000-0000-0000-0000-000000000000
AND
QRY_OncePerUserAndNearbyPlayers((CHARACTER)_Player,"CRE_WeaponsPuzzle_RemovedWeaponReaction")
THEN
StartVoiceBark((VOICEBARKRESOURCE)CRE_WeaponsPuzzle_VB_RemovedWeaponReaction_d3e5a193-f1d6-79c8-e560-466744b2da18, _Player);

//Turn on light
PROC
PROC_CRE_WeaponsPuzzle_TurnOnLight((TRIGGER)_Trigger)
AND
DB_CRE_WeaponPuzzle_Lights(_Trigger, _On1, _On2, _Off1, _Off2)
THEN
PROC_SetOnStage(_On1, 1);
PROC_SetOnStage(_On2, 1);
PROC_SetOnStage(_Off1, 0);
PROC_SetOnStage(_Off2, 0);
PROC_LoopEffect(VFX_Script_CRE_Lathander_Platform_A_Crystal_B_01_8677ebf4-fc2c-17e0-bfad-624406962341, _On1, "CRE_BloodOfLathander_Lance_GlowingCrystals", "CRE_Main_A", "", 1.0);
PROC_LoopEffect(VFX_Script_CRE_Lathander_Platform_A_Crystal_B_01_8677ebf4-fc2c-17e0-bfad-624406962341, _On2, "CRE_BloodOfLathander_Lance_GlowingCrystals", "CRE_Main_A", "", 1.0);

//Turn off light
PROC
PROC_CRE_WeaponsPuzzle_TurnOffLight((TRIGGER)_Trigger)
AND
DB_CRE_WeaponPuzzle_Lights(_Trigger, _On1, _On2, _Off1, _Off2)
THEN
PROC_SetOnStage(_On1, 0);
PROC_SetOnStage(_On2, 0);
PROC_SetOnStage(_Off1, 1);
PROC_SetOnStage(_Off2, 1);


//TODO: Edge-case: If there are multiple weapons of the correct type on the pedestal removing one shouldn't end the success condition

//Checking if all the weapons are in the correct location and revealing the reward.
IF
DB_GlobalFlag(CRE_WeaponsPuzzle_HasSpear_d9c14531-51c5-4796-b29b-f44b1ab384c7)
AND
DB_GlobalFlag(CRE_WeaponsPuzzle_HasWarhammer_e813343c-bd7e-41c9-b0d8-db5b9ea57dbf)
AND
DB_GlobalFlag(CRE_WeaponsPuzzle_HasBattleaxe_3049751e-9841-4b34-b8ee-8950d80bf8a9)
AND
DB_GlobalFlag(CRE_WeaponsPuzzle_HasLongsword_3cc7a940-0ecd-44dd-b04c-ebfe4562e56e)
AND
QRY_OnlyOnce("CRE_WeaponsPuzzle_GiveReward")
THEN
PROC_CRE_WeaponsPuzzle_GiveReward();

PROC
PROC_CRE_WeaponsPuzzle_GiveReward()
AND
DB_Players(_Player)
THEN
PROC_CRE_WeaponsPuzzle_OpenHiddenCompartment();
ApplyStatus(_Player, "CRE_MAG_LATHANDER_PUZZLE_BOON", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);
QuestUpdate(_Player, "HIDDEN_CRE_Boosters", "CRE_WeaponsPuzzle_SolvedPuzzle");
TemplateAddTo((ITEMROOT)WPN_HUM_Longsword_HolyAvenger_4cbfe982-bbc2-4848-aea7-231e74b6ba13, _Player, 1, 1);

PROC
PROC_CRE_WeaponsPuzzle_OpenHiddenCompartment()
AND
QRY_OnlyOnce("CRE_WeaponsPuzzle_OpenedHiddenDoor")
THEN
ItemMoveTo(S_CRE_WeaponsPuzzle_HiddenDoor_5e2d9891-0dfb-4d00-9691-6ed69a50c58a, S_CRE_WeaponsPuzzle_HiddenDoor_MoveToPoint_ee34da09-14bb-426d-9417-2012f818ec1d, 0.5, 1.0, 0, "CRE_WeaponsPuzzle_SecretDoorOpened");
PROC_SetOnStage(S_CRE_WeaponsPuzzle_Bag_fe520e48-2caa-421d-948c-30fa84c690ce, 1);

IF
Unlocked(S_CRE_WeaponsPuzzle_HiddenDoor_5e2d9891-0dfb-4d00-9691-6ed69a50c58a, _, _)
THEN
PROC_CRE_WeaponsPuzzle_OpenHiddenCompartment();

//REGION Reaction VB after reading the lat Dawnmaster's note left with the key 
IF
UseFinished(_Player, S_CRE_WeaponsPuzzle_Note_4c5bbc63-61f4-43f1-b2a1-de29071fd87c, 1)
AND
QRY_OncePerUserAndNearbyPlayers((CHARACTER)_Player,"CRE_Monastery_HiddenNoteReaction")
THEN
StartVoiceBark((VOICEBARKRESOURCE)CRE_WeaponsPuzzle_VB_HiddenNoteReaction_18caef9b-1aaa-2112-50db-1d3e2aceda7a, _Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1b"
