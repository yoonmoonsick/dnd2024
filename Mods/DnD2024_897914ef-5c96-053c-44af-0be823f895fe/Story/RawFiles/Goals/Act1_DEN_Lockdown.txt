Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Barrier
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_000_b740a6f0-6f71-4ff9-a765-d00c58a3cedf);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_001_77fd7000-5dd7-4c95-aedf-7d5a0de71981);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_002_507a10e6-15a7-4156-ade4-41a3fc6f379d);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_003_a3406535-3ba2-4db5-9f17-f58ec940624e);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_004_f727c2ea-2de9-4f14-a759-2f9f10c0894f);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_006_44a586a0-bc6c-482a-b962-291dc2fad3e3);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_005_a48979e2-6f73-49f2-be35-591eba4f326b);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_007_f79bcdac-8c2c-4c6d-a37b-9bd2c9033c7c);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_008_a1b9024f-7bd9-4c19-8f8b-4f380cf17656);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_009_149c02bb-82a7-4a13-8ec1-78b8aeffe04a);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_010_cb4ce5b6-1784-43f8-b601-dcdc3c4201a7);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_011_b28773f9-76ca-4120-892b-45e3e3f256ad);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_012_2c4381b3-8ced-4550-a85b-574804d8f110);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_013_636e1dca-5f00-441f-93a5-2014277bfa02);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_014_d63d91d0-3d86-4a16-b44e-63447f6ee2c6);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_015_015eb3fb-347a-4a01-97e3-6abb07ada02c);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_016_3126e574-e37d-4d93-ba51-8f12023ac695);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_017_0a5e8c43-ed16-42ca-9168-35eff26a0bea);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_018_a6da5cea-daf6-4b9c-ac3f-ed5c6396cbd7);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_019_311a8800-bcfc-4d89-bfe8-e255a48f4cd2);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_020_f7f6d3b3-a208-4eb3-b009-3092c94f5187);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_021_3239e2fe-9817-48ea-9a2c-c2865f8f60cd);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_022_9fa07e00-c29d-4b76-9544-fdfd35d7f106);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_023_eee8bfaf-3419-48e2-88d8-419fed4e94f7);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_024_6b91439f-7c05-428a-8c9a-e0acfdb8b71a);
DB_DEN_Lockdown_Barrier(S_DEN_LockdownVines_025_81e0a76e-90b7-44b5-9297-d9a735257ff6);

DB_DEN_Lockdown_Effect(NAT_CursedLand_Vine_Set_Wall_A_40857d38-ccf1-4c5a-8b11-ea63577897b1,(EFFECTRESOURCE)VFX_Script_DruidGroveLockdown_NAT_CursedLand_Vine_Set_Wall_A_Growing_01_1906dae2-2117-7a9c-8aa4-e17c305f4bae,"Dummy_NAT_CursedLand_Vine_Set_Wall_a");
DB_DEN_Lockdown_Effect(NAT_CursedLand_Vine_Set_Wall_B_3e71299d-306a-4e29-9250-0c35c0cc1427,VFX_Script_DruidGroveLockdown_NAT_CursedLand_Vine_Set_Wall_B_Growing_01_17d74b42-3fbc-b684-4838-92514928f5d5,"Dummy_NAT_CursedLand_Vine_Set_Wall_B");
DB_DEN_Lockdown_Effect(NAT_CursedLand_Vine_Set_Wall_C_2112a317-3c08-4324-91ac-c9e3e34406ea,VFX_Script_DruidGroveLockdown_NAT_CursedLand_Vine_Set_Wall_C_Growing_01_9a6a461e-6a18-d946-94a7-01a05d309166,"Dummy_NAT_CursedLand_Vine_Set_Wall_c");

UnloadLevelTemplate(LT_BlockerDruidGrove_A_000_927e9e0c-9f37-4c0f-ac02-0e04d42fe5dc);
//END_REGION

PROC_DEN_Lockdown_Stage();
SetFlag(DEN_Lockdown_State_Ready_b81f5676-cc51-4ece-babb-9875f9bf548f,NULL_00000000-0000-0000-0000-000000000000);
KBSECTION
//REGION Debug

IF
TextEvent("killgrovetieflings")
AND
DB_DEN_NPC(_Char,_Role,_)
AND
DB_DEN_NPC((CHARACTER)_Char,_,_)
AND
IsTagged(_Char,TIEFLING_aaef5d43-c6f3-434d-b11e-c763290dbe0c,1)
THEN
Die(_Char,DEATHTYPE.DoT,0);

//END_REGION

//REGION Trigger

IF
TextEvent("lockdown")
THEN
PROC_State_Progress(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", "DEN_State_Lockdown");
//DEBUG^

PROC
PROC_State_Changed(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", _State)
AND
NOT QRY_DEN_CheckBeforeState("DEN_State_RitualStopped")
THEN
ClearFlag(DEN_Lockdown_State_Ready_b81f5676-cc51-4ece-babb-9875f9bf548f,NULL_00000000-0000-0000-0000-000000000000);

//END_REGION

//REGION Lockdown

QRY
QRY_DEN_Lockdown_IdolIsInGrove()
AND
DB_DEN_InDenTriggers(_Trigger)
AND
IsInTrigger(S_DEN_SilvanusIdol_a841d36c-9a00-4a26-943e-c0af6895bb16, _Trigger, 1)
THEN
DB_NOOP(1);


QRY
QRY_DEN_Lockdown_CanDoRitual()
AND
DB_GlobalFlag(DEN_Lockdown_State_Ready_b81f5676-cc51-4ece-babb-9875f9bf548f)
AND
QRY_DEN_Lockdown_IdolIsInGrove()
AND
DB_DEN_DruidAttack_Druids(_Druid, _, _)
AND
IsTagged(_Druid, BEAST_890b5a2a-e773-48df-b191-c887d87bec16, 0)
THEN
DB_NOOP(1);


PROC
PROC_DEN_Lockdown_Stage()
AND
DB_DEN_Lockdown_Barrier(_Item)
THEN
SetOnStage(_Item,0);

PROC
PROC_State_Changed(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", "DEN_State_Lockdown")
THEN
PROC_State_Progress(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", "DEN_State_LockdownComplete");
PROC_DEN_Lockdown();
PROC_DEN_Lockdown_JournalHook();

PROC
PROC_DEN_Lockdown_JournalHook()
THEN
DB_NOOP(1);

PROC
PROC_DEN_Lockdown()
AND
DB_DEN_Lockdown_Barrier(_Item)
AND
GetTemplate(_Item,_Root)
AND
DB_DEN_Lockdown_Effect(_Root,_Effect,_Bone)
THEN
SetOnStage(_Item,1);

PROC
PROC_DEN_Lockdown()
THEN
LoadLevelTemplate(LT_BlockerDruidGrove_A_000_927e9e0c-9f37-4c0f-ac02-0e04d42fe5dc);
PROC_SetRelationToPlayers(ACT1_DEN_Druids_ead8461a-07f4-4552-8398-f4a464536500,0);
PROC_LockWaypointForAll("WAYP_DEN_Camp");
PROC_LockWaypointForAll("WAYP_DEN_Camp_2");
PROC_ItemCloseAndLock(S_DEN_Gate_567055bf-ffeb-4236-8a61-e53877397fde,"Key_Null");
SetCanInteract(S_DEN_TunnelFrontOut_0dbc8db6-0808-49a8-a715-20323e48f44a,0);
Die(S_DEN_GateMechanism_5564d21c-9033-4958-8cc8-cd3f497bb51b);
ClearFlag(DEN_Lockdown_State_Ready_b81f5676-cc51-4ece-babb-9875f9bf548f,NULL_00000000-0000-0000-0000-000000000000);
SetFlag(DEN_Lockdown_State_Active_0b54c7d2-b7b1-4d0f-b8e4-0cf1ee32b1eb,NULL_00000000-0000-0000-0000-000000000000);
CreatePuddle(S_DEN_OutsideLeaveArea_bc528607-56ed-4f1b-9ae1-76c391b96d57,"SurfacePoison",100,1000,20,200,1.0);
DB_OneShot_VoiceBarkTrigger(S_DEN_LockdownVB_Front_e3e75f2d-a512-4b14-bd74-1ec9a0824af8,DEN_Lockdown_VB_Comment_b5afc10f-4aae-8660-00bf-f5338f6d4fde);
DB_OneShot_VoiceBarkTrigger(S_DEN_LockdownVB_Side_bf7800b2-e589-44ef-9066-2f393fb91811,DEN_Lockdown_VB_Comment_b5afc10f-4aae-8660-00bf-f5338f6d4fde);

PROC
PROC_DEN_Lockdown()
AND
DB_DEN_NPC(_Char,"",_Dialog)
THEN
PROC_DEN_AttackOnDen_SetupNPC(_Char,"",_Dialog);

PROC
PROC_DEN_Lockdown()
AND
DB_DEN_NPC(_Char,_Role,_)
AND
_Role != ""
AND
NOT DB_PermaDefeated(_Char)
AND
NOT DB_DEN_Druids(_Char,_)
AND
IsTagged(_Char,HUMANOID_7fbed0d4-cabc-4a9d-804e-12ca6088a0a8,1)
AND
QRY_DEN_IsInDen(_Char)
THEN
Die(_Char,DEATHTYPE.DoT,0);

IF
AttackedBy(_Item,_,_Char,_,_,_,_)
AND
NOT DB_OnlyOnce("DEN_Lockdown_Attacked")
AND
DB_DEN_Lockdown_Barrier(_Item)
AND
DB_Players((CHARACTER)_Char)
AND
QRY_StartDialog_Fixed(DEN_Lockdown_PAD_Attack_498017d7-f37c-951f-15c4-35dc2e1c15f5,_Char)
THEN
DB_OnlyOnce("DEN_Lockdown_Attacked");
//END_REGION


//REGION Track story items in the grove to scatter them at the waypoint if lockdown occurs.
IF
DB_DEN_InDenTriggers(_Trigger)
THEN
TriggerRegisterForItems(_Trigger);

IF
ItemEnteredTrigger(_Item,_Trigger,_)
AND
DB_DEN_InDenTriggers(_Trigger)
AND
Exists(_Item,1)
AND
IsStoryItem(_Item,1)
THEN
DB_DEN_InDenStoryItem(_Item,_Trigger);

IF
ItemLeftTrigger(_Item,_Trigger,_)
AND
DB_DEN_InDenStoryItem(_Item,_Trigger)
THEN
NOT DB_DEN_InDenStoryItem(_Item,_Trigger);

IF
DestroyedBy(_Item,_,_,_)
AND
DB_DEN_InDenStoryItem(_Item,_Trigger)
THEN
NOT DB_DEN_InDenStoryItem(_Item,_Trigger);

IF
AddedTo(_Item,_Char, _)
AND
DB_Players((CHARACTER)_Char)
AND
DB_DEN_InDenStoryItem((ITEM)_Item,_Trigger)
THEN
NOT DB_DEN_InDenStoryItem(_Item,_Trigger);

PROC
PROC_DEN_Lockdown()
AND
DB_DEN_InDenTriggers(_Trigger)
THEN
PROC_TriggerUnregisterForItemsWhenInLevel(_Trigger,"WLD_Main_A");

PROC
PROC_DEN_Lockdown()
AND
GetPosition(S_CAMP_Waypoint_Act1a_Pos_c68269af-ceac-4f90-9224-040859fe6176,_X,_Y,_Z)
AND
DB_DEN_InDenStoryItem(_Item,_)
THEN
ScatterAt(_Item,_X,_Y,_Z);

PROC
PROC_DEN_Lockdown()
THEN
SysClear("DB_DEN_InDenStoryItem",2);
//END_REGION

//REGION Blocking den areas for return from camp when lockdown is active

PROC
PROC_State_Changed(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", "DEN_State_LockdownComplete")
AND
DB_DEN_InDenTriggers(_Trigger)
THEN
DB_LeaveCamp_ForbiddenReturnArea("WLD_Main_A", (TRIGGER)_Trigger, (TRIGGER)S_DEN_WaypointPos_cdd91969-67d0-454e-b27b-cf34e542956b);


//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
